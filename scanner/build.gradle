apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group='com.github.3plmax'

def DEFAULT_COMPILE_SDK_VERSION = 36
def DEFAULT_TARGET_SDK_VERSION  = 36
def DEFAULT_APPCOMPAT_VERSION = "1.7.1"

android {
    compileSdkVersion project.hasProperty('compileSdkVersion') ? project.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION

    defaultConfig {
        minSdkVersion 28
        targetSdkVersion project.hasProperty('targetSdkVersion') ? project.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
        ndk {
            abiFilters "armeabi", "armeabi-v7a"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    namespace 'com.shipitdone.scanner'
    buildFeatures {
        aidl true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    def appcompatVersion = project.hasProperty('appcompatVersion') ? project.appcompatVersion : DEFAULT_APPCOMPAT_VERSION
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation files('libs/zltd-n2s-scanner.jar')
    // 智联天地
    implementation files('libs/ScannerAPI.jar')
    // 东大集成
    implementation files('libs/platform_sdk_v2.4.0810.jar')
    // 优博讯
    implementation files('libs/SDK_release_v2.1.1.jar')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.3plmax'
            artifactId = 'scanner-sdk-manager'
            version = '0.0.1'
            afterEvaluate {
                from components.release
            }
        }
    }
}